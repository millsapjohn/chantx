<chant> ::= <word> (" " <word>)*
<word> ::= <syllable>+
<syllable> ::= <text> <annotation>+
<text> ::= ([a-z] | [A-Z] | "'" | "," | "." | ";" | ":" | "\"")*
<annotation> ::= "(" (<group> | <neume> | <rhythm>)+ ")"
<rhythm> ::= "|" | "||" | "|||" | "||||" | ";;"
<neume> ::= [0-9] ( "h" | "r" | "d" | "e" | "v" | "V" | "'" )*
<group> ::= <podatus> | <clivis> | <paren> | <brace> | <bracc> | <torculus> | <porrectus> | <scandicus> | <salicus> | <quilisma>
<podatus> ::= "p" "[" <neume> <neume> "]"
<clivis> ::= "c" "[" <neume> <neume> "]"
<paren> ::= "(" <neume> <neume>+ ")"
<brace> ::= "{" <neume> <neume>+ "}"
<bracc> ::= "{{" <neume> <neume>+ "}}"
<torculus> ::= "t" "[" <neume> <neume> <neume> "]"
<porrectus> ::= "o" "[" <neume> <neume> <neume> "]"
<scandicus> ::= "s" "[" <neume> <neume> <neume> "]"
<salicus> ::= "l" "[" <neume> <neume> <neume> "]"
<quilisma> ::= "q" "[" <neume> <neume> <neume> "]"
